#Maintainer: Noob Zhang <17194552+zhanghua000 at users dot noreply dot github dot com>

pkgname="python-autoxuexiplaywright-git"
pkgver=3.0.8.r28.ge1b8614
pkgrel=1
pkgdesc="Auto finish something boring."
arch=("any")
url="https://github.com/AutoXuexiPlaywright/AutoXuexiPlaywright"
license=("GPL")
provides=("python-autoxuexiplaywright")
conflicts=("python-autoxuexiplaywright")
makedepends=("python-poetry" "python-installer" "git")
depends=(
    "python-pillow" "pyzbar" "python-qrcode" "python-playwright"
    "python-m3u8" "python-magic"
)
checkdepends=(
    "pyside6"
)
optdepends=(
    "pyside6: for GUI interface"
    "python-ffmpeg-jonghwanhyeon: for getting video on test page"
)
source=(
    "git+${url}"
    "autoxuexiplaywright.desktop"
    "autoxuexiplaywright.metainfo.xml")
sha256sums=('SKIP'
            '5ab5425ef45006354b67f85c7e7cbb1e45f30c69d83fd4c98c0434809a8d254b'
            '6cc83c7c4b47b8f446c5c52a9503d5d19cb83c3388068b004499e3bdf6d50e5a')

pkgver(){
    cd "${srcdir}/AutoXuexiPlaywright"
    git describe --tags --long | sed 's/v//;s/-/.r/;s/-/./g'
}
build(){
    cd "${srcdir}/AutoXuexiPlaywright"
    unset HTTP_PROXY HTTPS_PROXY http_proxy https_proxy
    poetry install --with=gui --with=dev
    poetry build
}
check(){
    cd "${srcdir}/AutoXuexiPlaywright"
    poetry run pytest --cov=autoxuexiplaywright
}
package(){
    cd "${srcdir}/AutoXuexiPlaywright"
    python -m installer --destdir="${pkgdir}" dist/*.whl
    install -Dm644 "${srcdir}/autoxuexiplaywright.desktop" \
        "${pkgdir}/usr/share/applications/org.autoxuexiplaywright.autoxuexiplaywright.desktop"
    install -Dm644 "${srcdir}/AutoXuexiPlaywright/autoxuexiplaywright/resources/icon.png" \
        "${pkgdir}/usr/share/icons/hicolor/32x32/apps/org.autoxuexiplaywright.autoxuexiplaywright.png"
    install -Dm644 "${srcdir}/autoxuexiplaywright.metainfo.xml" \
        "${pkgdir}/usr/share/metainfo/org.autoxuexiplaywright.autoxuexiplaywright.metainfo.xml"
}
