#Maintainer: Noob Zhang <17194552+zhanghua000 at users dot noreply dot github dot com>

pkgname="python-autoxuexiplaywright-git"
pkgver=3.0.0.r2.g2bc0398
pkgrel=1
pkgdesc="Auto finish something boring."
arch=("any")
url="https://github.com/AutoXuexiPlaywright/AutoXuexiPlaywright"
license=("GPL")
provides=("python-autoxuexiplaywright")
conflicts=("python-autoxuexiplaywright")
makedepends=("python-poetry" "python-installer" "git")
depends=(
    "python-pillow" "pyzbar" "python-qrcode" "python-playwright"
    "python-m3u8" "python-magic"
)
checkdepends=(
    "pyside6"
)
optdepends=(
    "pyside6: for GUI interface"
    "python-ffmpeg-jonghwanhyeon: for getting video on test page"
)
source=(
    "git+${url}"
    "autoxuexiplaywright.desktop")
sha256sums=('SKIP'
            '220eccba3090274aa8bb47a37399d923bbec2a0da92c265ab6efc500a6676fb0')

pkgver(){
    cd "${srcdir}/AutoXuexiPlaywright"
    git describe --tags --long | sed 's/v//;s/-/.r/;s/-/./g'
}
build(){
    cd "${srcdir}/AutoXuexiPlaywright"
    poetry install --with=gui --with=dev
    poetry build
}
check(){
    cd "${srcdir}/AutoXuexiPlaywright"
    poetry run pytest --cov=autoxuexiplaywright
    poetry run pycycle --here
}
package(){
    cd "${srcdir}/AutoXuexiPlaywright"
    mkdir -p "${pkgdir}/usr/share/icons/hicolor/32x32/apps"
    python -m installer --destdir="${pkgdir}" dist/*.whl
    install -Dm644 "${srcdir}/autoxuexiplaywright.desktop" \
        "${pkgdir}/usr/share/applications/org.autoxuexiplaywright.autoxuexiplaywright.desktop"
    install -Dm644 "${srcdir}/AutoXuexiPlaywright/autoxuexiplaywright/resources/icon.png" \
        "${pkgdir}/usr/share/icons/hicolor/32x32/apps/autoxuexiplaywright.png" 
}
