#Maintainer: Noob Zhang <17194552+zhanghua000 at users dot noreply dot github dot com>

pkgname="python-autoxuexiplaywright"
pkgver=2.1.3.r53.gd983803
pkgrel=1
pkgdesc="Auto finish something boring."
arch=("any")
url="https://github.com/AutoXuexiPlaywright/AutoXuexiPlaywright"
license=("GPL")
makedepends=("python-poetry" "python-installer" "git" "imagemagick")
depends=(
    "python-aiohttp" "python-pillow" "pyzbar" "python-qrcode" 
    "python-requests" "python-playwright")
optdepends=("pyside6: for GUI interface")
source=(
    "git+${url}"
    "icon.ico::https://www.xuexi.cn/favicon.ico"
    "autoxuexiplaywright.desktop")
sha256sums=('SKIP'
            'ac95e6329fb7a52dc4d43372eca62a56c12eb3b3fb943ba463871e538b51735c'
            'bc4e858948e8e4f097304aa7070844951bf88d806919761a9722ed6cc8ce0074')

pkgver(){
    cd "${srcdir}/AutoXuexiPlaywright"
    git describe --tags --long | sed 's/v//;s/-/.r/;s/-/./g'
}
build(){
    magick icon.ico -resize 32x32 icon.png
    cd "${srcdir}/AutoXuexiPlaywright"
    poetry build
}
package(){
    
    cd "${srcdir}/AutoXuexiPlaywright"
    mkdir -p "${pkgdir}/usr/share/icons/hicolor/32x32/apps"
    python -m installer --destdir="${pkgdir}" dist/*.whl
    install -Dm644 "${srcdir}/autoxuexiplaywright.desktop" \
        "${pkgdir}/usr/share/applications/autoxuexiplaywright.desktop"
    install -Dm644 "${srcdir}/icon.png" \
        "${pkgdir}/usr/share/icons/hicolor/32x32/apps/autoxuexiplaywright.png" 
    
}
